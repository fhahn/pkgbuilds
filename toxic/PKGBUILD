# Maintainer: Florian Hahn <flo@fhahn.com>

pkgname=toxic-git
pkgdesc='CLI client for tox, a Skype replacement that respects your privacy'
license=('GPL3')
pkgver=0.r1134.efe61e3
pkgrel=1
depends=('tox-git' 'ncurses' 'libconfig')
makedepends=('git')
conflicts=('toxic')
provides=('toxic')
arch=('i686' 'x86_64')
url=('http://tox.im')
source=('git+https://github.com/Tox/toxic.git')
md5sums=('SKIP')
optdepends=(
  'openal: audio notifications'
  'freealut: audio notifications'
)

_gitname=toxic

pkgver() {
    cd "${srcdir}/${_gitname}"
    printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_gitname}/build"
    make PREFIX=/usr
}

package() {
    cd "${srcdir}/${_gitname}/build"
    make PREFIX=/usr DESTDIR="${pkgdir}" install
}
