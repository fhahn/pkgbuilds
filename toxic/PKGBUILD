# Maintainer: Florian Hahn <flo@fhahn.com>

pkgname=toxic-git
pkgdesc='CLI client for tox, a Skype replacement that respects your privacy'
license=('GLPv3')
pkgver=0.r827.de4a28c
pkgrel=1
depends=('tox-git' 'ncurses')
makedepends=('autoconf' 'git')
conflicts=('toxic')
provides=('toxic')
arch=('i686' 'x86_64')
url=('http://tox.im')
source=('git+https://github.com/Tox/toxic.git')
md5sums=('SKIP')
_gitname=toxic

pkgver() {
    cd "${srcdir}/${_gitname}"
    printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_gitname}"
    autoreconf -i
    ./configure --prefix=/usr --with-libtoxcore-headers=/usr/include/tox 
    make
}

package() {
    cd "${srcdir}/${_gitname}/build"
    make DESTDIR="${pkgdir}" install
    mkdir -p "${pkgdir}/usr/share/toxic/"
    install -Dm644 ../misc/DHTnodes "${pkgdir}/usr/share/toxic/"
}
