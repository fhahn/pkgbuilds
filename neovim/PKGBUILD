# Maintainer: Florian Hahn <flo@fhahn.com>

pkgname=neovim-git
pkgdesc="Vim's rebirth for the 21th century"
license=('custom:vim')
pkgver=0.r37.3d171b6
pkgrel=1
depends=('libuv' 'ncurses')
makedepends=('cmake' 'git')
conflicts=('')
provides=('neovim')
arch=('i686' 'x86_64')
url=('https://github.com/neovim/neovim')
source=('git+https://github.com/neovim/neovim.git')
md5sums=('SKIP')
_gitname=neovim

pkgver() {
  cd "${srcdir}/${_gitname}"
  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"
  mkdir build && cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  install -Dm755 "${srcdir}/${_gitname}/build/src/vim" "${pkgdir}/usr/bin/neovim"
  install -Dm644 "${srcdir}/${_gitname}/vim-license.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE" 
}

# vim:set sw=2 sts=2 et:
