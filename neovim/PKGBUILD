# Maintainer: Florian Hahn <flo@fhahn.com>

pkgname=neovim-git
_gitname=neovim
pkgdesc="Vim's rebirth for the 21st century"
license=('custom:neovim')
pkgver=0.r1365.66bc131
pkgrel=1
depends=('libuv' 'ncurses')
makedepends=('cmake' 'git' 'unzip')
conflicts=('')
provides=('neovim')
arch=('i686' 'x86_64')
url=('https://github.com/neovim/neovim')
source=('git+https://github.com/neovim/neovim.git')
md5sums=('SKIP')
install=neovim-git.install
optdepends=(
  'vim-runtime: provides useful .vim files, although some may not work with neovim'
)

pkgver() {
  cd "${srcdir}/${_gitname}"
  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"
  make cmake CMAKE_EXTRA_FLAGS="-DCMAKE_INSTALL_PREFIX=/usr"
  make
}

package() {
  install -Dm755 "${srcdir}/${_gitname}/build/bin/nvim" "${pkgdir}/usr/bin/nvim"
  install -Dm644 "${srcdir}/${_gitname}/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE" 
}

# vim:set sw=2 sts=2 et:
