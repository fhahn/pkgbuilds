# Maintainer: Florian Hahn <flo@fhahn.com>

pkgname=neovim-git
pkgdesc="Vim's rebirth for the 21st century"
license=('custom:neovim')
pkgver=0.r1726.bd1ecad
pkgrel=1
depends=('libuv' 'ncurses' 'msgpack-git-neovim' 'perl')
makedepends=('cmake' 'git' 'unzip' 'luajit' 'lua' 'lua-messagepack' 'lua-bitop' 'lua-lpeg')
provides=('neovim')
arch=('i686' 'x86_64')
url=('https://github.com/neovim/neovim')
source=("$pkgname::git://github.com/neovim/neovim.git")
md5sums=('SKIP')
install=neovim-git.install
optdepends=(
  'vim-runtime: provides useful .vim files, although some may not work with neovim'
)

pkgver() {
  cd "${pkgname}"
  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${pkgname}"
  cmake -G 'Unix Makefiles' -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${pkgname}"
  DESTDIR="${pkgdir}" make install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set sw=2 sts=2 et:
